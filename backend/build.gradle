plugins {
    id 'org.springframework.boot' version '2.1.7.RELEASE'
    id 'org.asciidoctor.convert' version '1.5.8'
    id 'java'
}

apply plugin: 'io.spring.dependency-management'

version = "${buildVersion}"

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

ext {
    snippetsDir = file('build/generated-snippets')
}

test {
    dependsOn ':frontend:karmaTests'
    outputs.dir snippetsDir
}

asciidoctor {
    dependsOn test
    sourceDir 'src/main/doc'
    attributes 'snippets': snippetsDir, 'version': version
    inputs.dir snippetsDir
}

bootJar {
    archiveFileName = "${rootProject.name}.jar"
    destinationDirectory = file("${rootProject.rootDir}/build")
    dependsOn asciidoctor
    dependsOn ':frontend:npmBuild', ':frontend:moveAssets'
    from ("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
    from ("${project(':frontend').projectDir}/dist") {
        into 'public'
    }
    manifest {
        attributes('Implementation-Version': version, 'Build-Date': new Date(), 'Created-By': "Gradle ${gradle.gradleVersion}")
    }
}

repositories {
    jcenter()
}

dependencies {
    // Spring Boot Starters
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-batch'
    runtimeOnly 'org.springframework.boot:spring-boot-devtools'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    // Spring Batch integration
    implementation 'org.springframework.batch:spring-batch-integration'

    // H2
    runtimeOnly 'com.h2database:h2'

    // PostgreSQL
    runtimeOnly 'org.postgresql:postgresql'

    // Jaxb
    implementation 'javax.xml.bind:jaxb-api'

    // EhCache
    implementation 'net.sf.ehcache:ehcache'

    // Flyway
    implementation 'org.flywaydb:flyway-core'

    // Apache Commons
    implementation 'org.apache.commons:commons-collections4:4.2'

    // Testing
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.springframework.batch:spring-batch-test'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
}

task stage {
    dependsOn build
}
