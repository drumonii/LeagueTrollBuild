plugins {
    id 'org.springframework.boot' version '1.5.9.RELEASE'
    id 'org.asciidoctor.convert' version '1.5.6'
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'

version = '2.3.13'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

eclipse {
    classpath {
        containers.remove 'org.eclipse.jdt.launching.JRE_CONTAINER'
        containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
    }
}

idea {
    module {
        inheritOutputDirs = false
        outputDir = file("$buildDir/classes/main/")
    }
}

ext {
    snippetsDir = file('build/generated-snippets')
}

test {
    outputs.dir snippetsDir
}

asciidoctor {
    dependsOn test
    sourceDir 'src/main/doc'
    attributes 'snippets': snippetsDir, 'version': version
    inputs.dir snippetsDir
}

jar {
    dependsOn asciidoctor
    from ("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
    manifest {
        attributes('Implementation-Version': version, 'Build-Date': new Date(), 'Created-By': "Gradle ${gradle.gradleVersion}")
    }
    exclude 'webapp'
}

bootRun {
    systemProperty 'spring.profiles.active', System.properties['spring.profiles.active']
    systemProperty 'riot.api.key', System.properties['riot.api.key']
    addResources = true
}

ext['assertj.version'] = '3.8.0'
ext['thymeleaf.version'] = '3.0.9.RELEASE'
ext['thymeleaf-extras-springsecurity4.version'] = '3.0.2.RELEASE'

repositories {
    jcenter()
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.group == 'nz.net.ultraq.thymeleaf' && details.requested.name == 'thymeleaf-layout-dialect') {
            details.useTarget 'com.github.zhanhb:thymeleaf-layout-dialect:2.2.1'
        }
    }
}

dependencies {
    // Spring Boot Starters
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-data-jpa'
    compile 'org.springframework.boot:spring-boot-starter-data-rest'
    compile 'org.springframework.boot:spring-boot-starter-cache'
    compile 'org.springframework.boot:spring-boot-starter-security'
    compile 'org.springframework.boot:spring-boot-starter-thymeleaf'
    compile 'org.springframework.boot:spring-boot-starter-actuator'
    compile 'org.springframework.boot:spring-boot-starter-batch'
    compile 'org.springframework.boot:spring-boot-devtools'
    compile 'eu.michael-simons:wro4j-spring-boot-starter:0.3.9'
    compileOnly 'org.springframework.boot:spring-boot-configuration-processor'

    // Lombok
    compileOnly 'org.projectlombok:lombok'

    // Thymeleaf Extras
    compile 'org.thymeleaf.extras:thymeleaf-extras-springsecurity4'

    // H2
    runtime 'com.h2database:h2'

    // PostgreSQL
    runtime 'org.postgresql:postgresql:42.1.4'

    // EhCache
    compile 'net.sf.ehcache:ehcache'

    // Flyway
    compile 'org.flywaydb:flyway-core'

    // Hibernate Java 8
    compile 'org.hibernate:hibernate-java8'

    // Jackson Datatypes
    compile 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'

    // Apache Commons
    compile 'org.apache.commons:commons-collections4:4.1'

    // WebJars Locator
    compile 'org.webjars:webjars-locator'

    // jQuery
	runtime 'org.webjars.npm:jquery:3.2.1'

    // DataTables
	runtime 'org.webjars.npm:datatables.net:1.10.16'
	// runtime 'org.webjars.npm:datatables.net-se:1.10.16'

    // Isotope
	runtime('org.webjars.npm:isotope-layout:3.0.5') {
		exclude group: 'org.webjars.npm'
	}

    // Clipboard JS
	runtime('org.webjars.npm:clipboard:1.7.1') {
		exclude group: 'org.webjars.npm'
	}

    // Testing
    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'org.springframework.security:spring-security-test'
    testCompile 'org.springframework.batch:spring-batch-test'
    testCompile 'org.springframework.restdocs:spring-restdocs-mockmvc'
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.3.1'
}

task stage {
    dependsOn build
}