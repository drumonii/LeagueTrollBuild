description = 'League of Legends Troll Build'

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'propdeps'
apply plugin: 'org.asciidoctor.convert'

version = '2.3.6'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

buildscript {
    repositories {
        jcenter()
        maven { url 'http://repo.spring.io/plugins-release' }
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.4.2.RELEASE'
        classpath 'org.springframework.build.gradle:propdeps-plugin:0.0.7'
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.3'
    }
}

eclipse {
    classpath {
        containers.remove 'org.eclipse.jdt.launching.JRE_CONTAINER'
        containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
    }
}

idea {
    module {
        inheritOutputDirs = false
        outputDir = file("$buildDir/classes/main/")
    }
}

jar {
    manifest {
        attributes('Implementation-Version': version, 'Build-Date': new Date(), 'Created-By': "Gradle ${gradle.gradleVersion}")
    }
}

ext {
    snippetsDir = file('build/generated-snippets')
}

test {
    outputs.dir snippetsDir
}

asciidoctor {
    sourceDir 'src/main/doc'
    attributes 'snippets': snippetsDir
    inputs.dir snippetsDir
    dependsOn test
}

jar {
    dependsOn asciidoctor
    from ("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
}

bootRun {
    systemProperty 'spring.profiles.active', System.properties['spring.profiles.active']
    systemProperty 'riot.api.key', System.properties['riot.api.key']
    addResources = true
}

ext['assertj.version'] = '3.5.2'
ext['thymeleaf.version'] = '3.0.2.RELEASE'
ext['thymeleaf-extras-springsecurity4.version'] = '3.0.0.RELEASE'
ext['thymeleaf-layout-dialect.version'] = '2.0.3'

repositories {
    jcenter()
}

dependencies {
    // Spring Boot Starters
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-data-jpa'
    compile 'org.springframework.boot:spring-boot-starter-data-rest'
    compile 'org.springframework.boot:spring-boot-starter-cache'
    compile 'org.springframework.boot:spring-boot-starter-security'
    compile 'org.springframework.boot:spring-boot-starter-thymeleaf'
    compile 'org.springframework.boot:spring-boot-starter-actuator'
    compile 'org.springframework.boot:spring-boot-starter-batch'
    optional 'org.springframework.boot:spring-boot-devtools'

    // Lombok
    compile 'org.projectlombok:lombok'

    // Thymeleaf Extras
    compile 'org.thymeleaf.extras:thymeleaf-extras-springsecurity4'

    // H2
    runtime 'com.h2database:h2'

    // PostgreSQL
    runtime 'org.postgresql:postgresql:9.4.1211'

    // EhCache
    compile 'net.sf.ehcache:ehcache'

    // Flyway
    compile 'org.flywaydb:flyway-core'

    // Apache Commons
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'org.apache.commons:commons-collections4:4.1'
    compile 'commons-io:commons-io:2.4'

    // WebJars Locator
    compile 'org.webjars:webjars-locator'

    // jQuery
    compile 'org.webjars.bower:jquery:2.2.4'

    // DataTables
    compile('org.webjars.bower:datatables:1.10.12') {
        exclude group: 'org.webjars.bower'
    }

    // Semantic UI
    compile('org.webjars.bower:semantic-ui:2.2.6') {
        exclude group: 'org.webjars.bower'
    }

    // Isotope
    compile('org.webjars.bower:isotope:3.0.1') {
        exclude group: 'org.webjars.bower'
    }

    // Clipboard JS
    compile 'org.webjars.bower:clipboard:1.5.15'

    // Testing
    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'org.springframework.security:spring-security-test'
    testCompile 'org.springframework.batch:spring-batch-test'
    testCompile 'org.springframework.restdocs:spring-restdocs-mockmvc'
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.1'
}

task stage {
    dependsOn build
}