plugins {
    id 'org.springframework.boot' version '2.0.0.RELEASE'
	id 'io.spring.dependency-management' version '1.0.4.RELEASE'
    id 'org.asciidoctor.convert' version '1.5.6'
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'

version = '2.5.0-SNAPSHOT'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

ext {
    snippetsDir = file('build/generated-snippets')
}

test {
    outputs.dir snippetsDir
}

asciidoctor {
    dependsOn test
    sourceDir 'src/main/doc'
    attributes 'snippets': snippetsDir, 'version': version
    inputs.dir snippetsDir
}

bootJar {
    dependsOn asciidoctor
    from ("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
    manifest {
        attributes('Implementation-Version': version, 'Build-Date': new Date(), 'Created-By': "Gradle ${gradle.gradleVersion}")
    }
    exclude 'webapp'
}

repositories {
    jcenter()
}

dependencies {
    // Spring Boot Starters
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-data-jpa'
    compile 'org.springframework.boot:spring-boot-starter-cache'
    compile 'org.springframework.boot:spring-boot-starter-security'
    compile 'org.springframework.boot:spring-boot-starter-thymeleaf'
    compile 'org.springframework.boot:spring-boot-starter-actuator'
    compile 'org.springframework.boot:spring-boot-starter-batch'
    compile 'org.springframework.boot:spring-boot-devtools'
    compile 'eu.michael-simons:wro4j-spring-boot-starter:0.4.1'
    compileOnly 'org.springframework.boot:spring-boot-configuration-processor'

    // Lombok
    compileOnly 'org.projectlombok:lombok'

    // Thymeleaf Extras
    compile 'org.thymeleaf.extras:thymeleaf-extras-springsecurity4'
    compile 'com.github.zhanhb:thymeleaf-layout-dialect:2.3.0'

    // H2
    runtime 'com.h2database:h2'

    // PostgreSQL
    runtime 'org.postgresql:postgresql:42.2.1'

    // EhCache
    compile 'net.sf.ehcache:ehcache'

    // Flyway
    compile 'org.flywaydb:flyway-core'

    // Apache Commons
    compile 'org.apache.commons:commons-collections4:4.1'

    // WebJars Locator
    compile 'org.webjars:webjars-locator-core'

    // jQuery
	runtime 'org.webjars.npm:jquery:3.2.1'

    // DataTables
	runtime 'org.webjars.npm:datatables.net:1.10.16'
	// runtime 'org.webjars.npm:datatables.net-se:1.10.16'

    // Isotope
	runtime('org.webjars.npm:isotope-layout:3.0.5') {
		exclude group: 'org.webjars.npm'
	}

    // Clipboard JS
	runtime('org.webjars.npm:clipboard:1.7.1') {
		exclude group: 'org.webjars.npm'
	}

    // Testing
    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'org.springframework.security:spring-security-test'
    testCompile 'org.springframework.batch:spring-batch-test'
    testCompile 'org.springframework.restdocs:spring-restdocs-mockmvc'
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.6'
}

task stage {
    dependsOn build
}