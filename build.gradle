description = 'League of Legends Troll Build'

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'io.spring.dependency-management'

version = '1.0-SNAPSHOT'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.2.7.RELEASE'
        classpath 'io.spring.gradle:dependency-management-plugin:0.5.3.RELEASE'
        classpath 'org.springframework:springloaded:1.2.4.RELEASE'
    }
}

eclipse {
    classpath {
        containers.remove 'org.eclipse.jdt.launching.JRE_CONTAINER'
        containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
    }
}

idea {
    module {
        inheritOutputDirs = false
        outputDir = file("$buildDir/classes/main/")
    }
}

jar {
    manifest {
        attributes('Implementation-Version': version, 'Build-Date': new Date(), 'Created-By': "Gradle ${gradle.gradleVersion}")
    }
}

bootRun {
    systemProperty 'spring.profiles.active', System.properties['spring.profiles.active']
    systemProperty 'riot.api.key', System.properties['riot.api.key']
}

repositories {
    jcenter()
}

dependencies {
    // Spring Boot Starters
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-data-jpa'
    compile 'org.springframework.boot:spring-boot-starter-data-rest'
    compile 'org.springframework.boot:spring-boot-starter-security'
    compile 'org.springframework.boot:spring-boot-starter-thymeleaf'

    // Lombok
    compile 'org.projectlombok:lombok:1.16.6'

    // Thymeleaf Extras
    compile 'org.thymeleaf.extras:thymeleaf-extras-springsecurity3'

    // H2
    compile 'com.h2database:h2'

    // PostgreSQL
    compile 'org.postgresql:postgresql:9.4-1204-jdbc42'

    // Flyway
    compile 'org.flywaydb:flyway-core'

    // Apache Commons
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'org.apache.commons:commons-collections4:4.0'
    compile 'commons-io:commons-io:2.4'

    // WebJars Locator
    compile 'org.webjars:webjars-locator:0.28'

    // jQuery
    compile 'org.webjars.bower:jquery:2.1.4'

    // DataTables
    compile 'org.webjars.bower:datatables:1.10.9'

    // Semantic UI
    compile 'org.webjars.bower:semantic:2.1.5'

    // Testing
    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'org.assertj:assertj-core:3.1.0'
    testCompile 'org.skyscreamer:jsonassert:1.2.3'
    testCompile 'com.jayway.jsonpath:json-path'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.8'
}
