description = 'League of Legends Troll Build'

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.asciidoctor.convert'

version = '2.2-SNAPSHOT'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.3.3.RELEASE'
        classpath 'io.spring.gradle:dependency-management-plugin:0.5.6.RELEASE'
        classpath 'org.springframework:springloaded:1.2.6.RELEASE'
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.3'
    }
}

eclipse {
    classpath {
        containers.remove 'org.eclipse.jdt.launching.JRE_CONTAINER'
        containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
    }
}

idea {
    module {
        inheritOutputDirs = false
        outputDir = file("$buildDir/classes/main/")
    }
}

jar {
    manifest {
        attributes('Implementation-Version': version, 'Build-Date': new Date(), 'Created-By': "Gradle ${gradle.gradleVersion}")
    }
}

ext {
    snippetsDir = file('build/generated-snippets')
}

test {
    outputs.dir snippetsDir
}

asciidoctor {
    sourceDir 'src/main/doc'
    attributes 'snippets': snippetsDir
    inputs.dir snippetsDir
    dependsOn test
}

bootRun {
    systemProperty 'spring.profiles.active', System.properties['spring.profiles.active']
    systemProperty 'riot.api.key', System.properties['riot.api.key']
    addResources = true
}

repositories {
    jcenter()
}

dependencies {
    // Spring Boot Starters
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-data-jpa'
    compile 'org.springframework.boot:spring-boot-starter-data-rest'
    compile 'org.springframework.boot:spring-boot-starter-cache'
    compile 'org.springframework.boot:spring-boot-starter-security'
    compile 'org.springframework.boot:spring-boot-starter-thymeleaf'
    compile 'org.springframework.boot:spring-boot-starter-actuator'
    compile 'org.springframework.boot:spring-boot-devtools'

    // Lombok
    compile 'org.projectlombok:lombok:1.16.8'

    // Thymeleaf Extras
    compile 'org.thymeleaf.extras:thymeleaf-extras-springsecurity4'

    // H2
    runtime 'com.h2database:h2'

    // PostgreSQL
    runtime 'org.postgresql:postgresql:9.4.1208'

    // EhCache
    compile 'net.sf.ehcache:ehcache'

    // Flyway
    compile 'org.flywaydb:flyway-core'

    // Apache Commons
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'org.apache.commons:commons-collections4:4.1'
    compile 'commons-io:commons-io:2.4'

    // WebJars Locator
    compile 'org.webjars:webjars-locator'

    // jQuery
    compile 'org.webjars.bower:jquery:2.2.1'

    // DataTables
    compile 'org.webjars.bower:datatables:1.10.11'

    // Semantic UI
    compile 'org.webjars.bower:semantic:2.1.8'

    // Searcher
    compile 'org.webjars.bower:jquery-searcher:0.3.0'

    // Testing
    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'org.springframework.security:spring-security-test'
    testCompile 'org.assertj:assertj-core:3.4.1'
    testCompile 'org.skyscreamer:jsonassert:1.3.0'
    testCompile 'com.jayway.jsonpath:json-path'
    testCompile 'org.springframework.restdocs:spring-restdocs-mockmvc'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.12'
}

task stage {
    dependsOn build
}